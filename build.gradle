import com.vanniktech.maven.publish.SonatypeHost

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.1.3"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.22.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    if (project.name != "app") {

        apply plugin: "com.vanniktech.maven.publish"

        group = "wang.harlon.quickjs"

        mavenPublishing {
            publishToMavenCentral(SonatypeHost.S01, true)

            signAllPublications()

            pom {
                name = "QuickJS Wrapper"
                description = "Quickjs wrapper library for Android/JVM."
                inceptionYear = "2022"
                url = "https://github.com/HarlonWang/quickjs-wrapper"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "HarlonWang"
                        name = "Harlon Wang"
                        url = "https://github.com/HarlonWang"
                    }
                }
                scm {
                    url = "https://github.com/HarlonWang/quickjs-wrapper"
                    connection = "scm:git:git://github.com/HarlonWang/quickjs-wrapper.git"
                    developerConnection = "scm:git:ssh://github.com/HarlonWang/quickjs-wrapper.git"
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}